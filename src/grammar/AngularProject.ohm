AngularProject {
  Application = name Contained<Module*>

  Module = name Contained<ModuleElement*>

  ModuleElement = Requirements
                | Components
                | Factories
                | Routes
                | Configs

  Requirements = RequirementsGlyph Contained<Requirement*>
  RequirementsGlyph = "requires"
                    | "<="
                    | "<"
  Requirement = name JSPackage?
  JSPackage = ":" jsPackageName

  Components = ComponentsGlyph Contained<Component*>
  ComponentsGlyph = "components"
                  | "directives"
                  | "#"
  Component = name Contained<Component*>?

  Factories = FactoriesGlyph Contained<Factory*>
  FactoriesGlyph = "factories"
                 | "^"
  Factory = name Contained<Factory*>?

  Routes = RoutesGlyph Contained<Route*>
  RoutesGlyph = "routes"
              | "=>"
              | ">"
  Route = Quoted<path> ":" name
  path = routeCharacter+
  routeCharacter = alnum | "/" | "_" | "-"

  Configs = "config" Contained<Config*>?
  Config = name

  Contained<element> = CContained<"{", element, "}">
  Quoted<element> = CContained<"'", element, "'">
  CContained<open, element, close> = open element close

  name = nameCharacter+
  nameCharacter = alnum | "_" | "-"

  jsPackageName = name
}